apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'and-res-guard.gradle'

def keyFile = file(findProperty("tianma.keystore.path") as String)
def propFile = file(findProperty("tianma.signature.path") as String)

def keyProps = new Properties()
if (propFile.exists()) {
    keyProps.load(new FileInputStream(propFile))
}

def static releaseTime() {
    return new Date().format("yyMMdd", TimeZone.default)
}

ext {
    VERSION_CODE = 17
    VERSION_NAME = "1.4.0"
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.tianma.tweaks.miui"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField("String", "LOG_TAG", "\"XMiTools\"")
        buildConfigField("boolean", "LOG_TO_XPOSED", "true")
        buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")
        buildConfigField("int", "MODULE_VERSION", "" + VERSION_CODE)
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile keyFile
            storePassword keyProps['STORE_PASSWORD']
            keyAlias keyProps['KEY_ALIAS']
            keyPassword keyProps['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            buildConfigField("int", "LOG_LEVEL", "2")

            if (keyFile.exists()) {
                println "using keystore"
                signingConfig signingConfigs.release
            }

            debuggable true
        }

        release {
            buildConfigField("int", "LOG_LEVEL", "4")

            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            if (keyFile.exists()) {
                println "using keystore"
                signingConfig signingConfigs.release
            }

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "XMiTools_${variant.versionName.replaceAll('\\s+', '_')}_${releaseTime()}_${variant.buildType.name.charAt(0)}.apk"
        }
    }
}

ext {
    gsonVersion = "2.8.9" // gson

    rxJavaVersion = "2.2.17" // RxJava 2
    rxAndroidVersion = "2.1.1" // RxAndroid 2

    okHttpVersion = "4.9.3" // OkHttp
    retrofitVersion = "2.9.0" // retrofit

    materialDialogsVersion = "3.3.0"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0' // material design support
    implementation 'androidx.preference:preference-ktx:1.2.0' // preference-v7
    implementation 'androidx.browser:browser:1.4.0' // custom tabs
    implementation "androidx.constraintlayout:constraintlayout:2.1.4" // constraint layout
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"

    // Xposed
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    // ButterKnife
    // implementation 'com.jakewharton:butterknife:10.2.0'
    // annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'

    // Android Shell
    implementation 'com.jaredrummler:android-shell:1.0.0'

    // Material Dialogs
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"

    // color picker preference
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // RxJava 2
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    // RxAndroid 2
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    // OkHttp logging interceptor
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    // Retrofit Gson converter
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    // Retrofit scalars converter
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    // Retrofit RxJava Adapter
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Kotlin
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // flex-box layout
    implementation 'com.google.android:flexbox:2.0.1'
}
repositories {
    mavenCentral()
}
